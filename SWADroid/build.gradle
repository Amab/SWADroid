apply plugin: 'com.android.application'

import org.ajoberstar.grgit.Grgit

ext {
    git = Grgit.open()
    gitVersionCode = git.tag.list().size()
    gitVersionName = "${git.describe()}"
}

//return a BuildConfigField from a properties file.
def static getBuildConfigField(String property){
    def travisBuild = System.getenv("TRAVIS") == "true"
    def value

    if(travisBuild) {
        value = "\"" + System.getenv(property) + "\""
    } else {
        Properties properties = new Properties()
        properties.load(new FileInputStream("keystore.properties"))
        value = "\"" + properties.getProperty(property) + "\""
    }

    return value
}

android {
    compileSdkVersion "android-P"

    dexOptions {
        maxProcessCount=2
        javaMaxHeapSize "2g"
    }

    lintOptions {
        abortOnError false
    }

    defaultConfig {
        applicationId "es.ugr.swad.swadroid"
        versionCode gitVersionCode
        versionName gitVersionName

        minSdkVersion 14
        targetSdkVersion "P"

        // TODO Remove commented code
        /*android.applicationVariants.all { variant ->
            def appName
            //Check if an applicationName property is supplied; if not use the name of the parent project.
            if (project.hasProperty("applicationName")) {
                appName = applicationName
            } else {
                appName = parent.name
            }

            variant.outputs.all { output ->
                outputFileName = "${appName}-${output.baseName}-${variant.versionName}.apk"
            }
        }*/

        testApplicationId "es.ugr.swad.swadroid.test"
        testInstrumentationRunner "android.test.InstrumentationTestRunner"
    }

    buildTypes {
        buildTypes.each {
            it.buildConfigField "String", "SWAD_APP_KEY", getBuildConfigField("SWAD_APP_KEY")
            it.buildConfigField "String", "ANALYTICS_API_KEY", getBuildConfigField("ANALYTICS_API_KEY")
        }
    }
}

// TODO Switch to release versions when Android P is released
dependencies {
    implementation 'com.android.support:support-v4:28.0.0-alpha1'
    implementation 'com.android.support:appcompat-v7:28.0.0-alpha1'
    implementation 'com.google.android.gms:play-services-analytics:15.0.2'
    implementation 'com.google.code.ksoap2-android:ksoap2-android:3.6.2'
    implementation 'commons-io:commons-io:2.6'
    implementation 'com.nostra13.universalimageloader:universal-image-loader:1.9.5'
    implementation 'com.journeyapps:zxing-android-embedded:3.5.0@aar'
    implementation 'com.google.zxing:core:3.3.2'
    implementation 'com.google.code.gson:gson:2.8.2'
}